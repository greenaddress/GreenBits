apply plugin: 'com.android.application'
android {
    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/io.netty.versions.properties'
        exclude 'META-INF/rxjava.properties'
        exclude 'lib/x86_64/darwin/libscrypt.dylib'
        exclude 'jackson-core-asl-1.8.6.jar:META-INF/ASL2.0'
        exclude 'jackson-core-asl-1.8.6.jar:META-INF/LICENSE'
        exclude 'jackson-core-asl-1.8.6.jar:META-INF/NOTICE'
        exclude 'org/bitcoinj/crypto/mnemonic/wordlist/english.txt'
        exclude 'org/bitcoinj/crypto/cacerts'
        exclude 'org.bitcoin.production.checkpoints.txt'
        exclude 'org.bitcoin.test.checkpoints.txt'
        exclude 'org/codehaus/jackson/impl/VERSION.txt'
        exclude 'org/codehaus/jackson/map/VERSION.txt'
        exclude 'org/fusesource/leveldbjni/version.txt'
        exclude 'org/h2/util/data.zip'
        exclude 'META-INF/native/linux32/libleveldbjni.so'
        exclude 'META-INF/native/linux64/libleveldbjni.so'
        exclude 'META-INF/native/osx/libleveldbjni.jnilib'
        exclude 'META-INF/native/windows32/leveldbjni.dll'
        exclude 'META-INF/native/windows64/leveldbjni.dll'
        exclude 'META-INF/services/java.sql.Driver'
        exclude 'com/google/thirdparty/publicsuffix/PublicSuffixPatterns.gwt.xml'
        exclude 'com/google/thirdparty/publicsuffix/PublicSuffixType.gwt.xml'
        exclude 'com/mysql/jdbc/Charsets.properties'
        exclude 'com/mysql/jdbc/LocalizedErrorMessages.properties'
        exclude 'com/mysql/jdbc/TimeZoneMapping.properties'
        exclude 'com/mysql/jdbc/configs/3-0-Compat.properties'
        exclude 'com/mysql/jdbc/configs/5-0-Compat.properties'
        exclude 'com/mysql/jdbc/configs/clusterBase.properties'
        exclude 'com/mysql/jdbc/configs/coldFusion.properties'
        exclude 'com/mysql/jdbc/configs/fullDebug.properties'
        exclude 'com/mysql/jdbc/configs/maxPerformance.properties'
        exclude 'com/mysql/jdbc/configs/solarisMaxPerformance.properties'
    }
    compileSdkVersion 26
    buildToolsVersion '27.0.3'

    defaultConfig {
        applicationId 'com.greenaddress.greenbits_android_wallet'
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 203
        // Don't use defaultConfig.getProperty("versionCode") in versionName
        // because it breaks F-Droid!
        versionName "2.03"
    }
    flavorDimensions 'normal'
    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            multiDexEnabled true
            minifyEnabled true
            shrinkResources true
            matchingFallbacks = ['normal']
        }
        debug {
            multiDexEnabled true
            matchingFallbacks = ['normal']
        }
    }
    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk true
        }
    }
    project.ext.versionCodes = ['armeabi-v7a': 2, 'arm64-v8a': 3, 'x86': 8, 'x86_64': 9]
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    variant.mergedFlavor.minSdkVersion.apiLevel * 1000000 + project.ext.versionCodes.get(output.getFilter(
                            com.android.build.OutputFile.ABI), 0) * 10000 + android.defaultConfig.versionCode
        }
    }
    productFlavors {
        production {
        }
        production_21 {
            minSdkVersion 21
        }
        btctestnet {
            applicationId "com.greenaddress.greenbits_android_wallet.testnet"
            versionName String.format("%s_testnet", defaultConfig.getProperty("versionName"))
        }
        btctestnet_21 {
            minSdkVersion 21
            applicationId "com.greenaddress.greenbits_android_wallet.testnet"
            versionName String.format("%s_testnet", defaultConfig.getProperty("versionName"))
        }
        publicregtest {
            applicationId "com.greenaddress.greenbits_android_wallet.publicregtest"
            versionName String.format("%s_publicregtest", defaultConfig.getProperty("versionName"))
        }
        publicregtest_21 {
            minSdkVersion 21
            applicationId "com.greenaddress.greenbits_android_wallet.publicregtest"
            versionName String.format("%s_publicregtest", defaultConfig.getProperty("versionName"))
        }
        regtest {
            applicationId "com.greenaddress.greenbits_android_wallet.regtest"
            versionName String.format("%s_regtest", defaultConfig.getProperty("versionName"))
        }
        regtest_21 {
            minSdkVersion 21
            applicationId "com.greenaddress.greenbits_android_wallet.regtest"
            versionName String.format("%s_regtest", defaultConfig.getProperty("versionName"))
        }
        elementsregtest {
            applicationId "com.greenaddress.greenbits_android_wallet.elementsregtest"
            versionName String.format("%s_elementsregtest", defaultConfig.getProperty("versionName"))
        }
        elementsregtest_21 {
            minSdkVersion 21
            applicationId "com.greenaddress.greenbits_android_wallet.elementsregtest"
            versionName String.format("%s_elementsregtest", defaultConfig.getProperty("versionName"))
        }
    }

    sourceSets {
        production_21 {
            java { srcDir 'src/production/java' }
            assets { srcDir 'src/production/assets' }
        }
        btctestnet_21 {
            java { srcDir 'src/btctestnet/java' }
            assets { srcDir 'src/btctestnet/assets' }
            res { srcDir 'src/btctestnet/res' }
        }
        publicregtest_21 {
            java { srcDir 'src/publicregtest/java' }
            assets { srcDir 'src/publicregtest/assets' }
            res { srcDir 'src/publicregtest/res' }
        }
        regtest_21 {
            java { srcDir 'src/regtest/java' }
            assets { srcDir 'src/regtest/assets' }
            res { srcDir 'src/regtest/res' }
        }
        elementsregtest_21 {
            java { srcDir 'src/elementsregtest/java' }
            assets { srcDir 'src/elementsregtest/assets' }
            res { srcDir 'src/elementsregtest/res' }
        }
     }

    dexOptions {
        maxProcessCount 2
        javaMaxHeapSize "2048M"
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    lintOptions {
        disable 'MissingTranslation'
    }
}

configurations {
    api.exclude group: "org.json", module: "json"
}

dependencies {
    api project(':bitcoinj:core'), project(':jawampa'),
            project(':jawampa:jawampa-core'), project(':jawampa:jawampa-netty'),
            files('libwally-core/src/swig_java/wallycore.jar')
    api 'com.fasterxml.jackson:jackson-datatype-json-org:1.8.0'
    api 'com.github.dmytrodanylyk.circular-progress-button:library:1.1.3'
    api 'com.android.support:support-v4:25.3.1'
    // force material-dialogs to use latest support libs
    api 'com.android.support:support-v13:25.3.1'
    api 'com.android.support:support-annotations:25.3.1'
    api 'com.android.support:appcompat-v7:25.3.1'
    api 'com.android.support:design:25.3.1'
    api 'com.android.support:recyclerview-v7:25.3.1'
    api 'com.android.support:gridlayout-v7:25.3.1'
    api 'com.google.zxing:core:3.3.0'
    api 'com.google.protobuf:protobuf-java:3.3.0'
    api 'com.android.support:multidex:1.0.1'
    api 'com.squareup.okhttp:okhttp:2.7.5'
    api 'com.github.medyo:android-about-page:1.2.1'
    api 'com.afollestad.material-dialogs:core:0.9.4.7'
    // used to redirect bitcoinj logging to android in debug builds, enable manually
    // debugCompile 'com.noveogroup.android:android-logger:1.3.6'
}

repositories {
    jcenter()
    maven {
        url "https://maven.google.com"
    }
}
